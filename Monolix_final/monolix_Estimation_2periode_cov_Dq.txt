DESCRIPTION: FIT SEIRAH MODEL 2 PERIOD

[LONGITUDINAL]
INPUT: 
parameter = {b1,E0,testing,beta} 
regressor = {init_I0,init_H0,isolation,popsize,ascertainment}

EQUATION:

odeType = stiff	
alpha=1.5
Dh=30
Di=2.3
De=5.2
t0 = 0  ; t0 is a reserved keyword (initiation of therapy)

H_0=init_H0
I_0=init_I0
E_0=E0
R_0=0
A_0=testing*E0
S_0=popsize-E_0-I_0-R_0-A_0-H_0

transmission=b1*exp(beta*isolation)
Dq=2
dailyMove=(1-isolation)*0.01*popsize


ddt_S = -transmission*S*(I+alpha*A)/popsize+dailyMove-dailyMove*S/(popsize-I-H)
ddt_E = transmission*S*(I+alpha*A)/popsize-E/De-dailyMove*E/(popsize-I-H)
ddt_I = ascertainment*E/De-I/Dq-I/Di
ddt_R = (I+A)/Di+H/Dh-dailyMove*R/(popsize-I-H)
ddt_A = (1-ascertainment)*E/De-A/Di-dailyMove*A/(popsize-I-H)
ddt_H = I/Dq-H/Dh

Isim=ascertainment*E/De
Hsim=I/Dq

DEFINITION:
Y1= {type = count,  log(P(Y1=k)) = -Isim+ k*log(Isim) - factln(k) }
Y2= {type = count,  log(P(Y2=k)) = -Hsim + k*log(Hsim) - factln(k) }

OUTPUT:

output = {Y1, Y2}
